plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
		name = "meteor-maven"
		url = "https://maven.meteordev.org/releases"
	}
    maven {
        name = "meteor-maven-snapshots"
        url = "https://maven.meteordev.org/snapshots"
    }
    maven {
        url "https://gitlab.com/api/v4/projects/25805200/packages/maven"
    }
    maven {
        name = "vram"
        url = "https://maven.vram.io/"
    }
    mavenCentral()
}

configurations {
    // include mods
    modImplementation.extendsFrom(modInclude)
    include.extendsFrom(modInclude)
}

dependencies {
	// Fabric
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API
    modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fapi_version}")
    shadow(fabricApi.module("fabric-api-base", project.fapi_version)) {
        transitive = false
    }

    // Compat fixes
    modCompileOnly("me.jellysquid.mods:sodium-fabric:${project.sodium_version}") { transitive = false }
    modCompileOnly("io.vram:canvas-fabric-mc119:1.0.+") { transitive = false }

	// Baritone (https://github.com/MeteorDevelopment/baritone)
    modInclude "baritone:fabric:${project.minecraft_version}-SNAPSHOT"

    // Libraries
    modInclude "meteordevelopment:orbit:${project.orbit_version}"
    modInclude "meteordevelopment:starscript:${project.starscript_version}"
    modInclude "org.reflections:reflections:${project.reflections_version}"
    modInclude("io.netty:netty-handler-proxy:${project.netty_version}") { transitive = false }
    modInclude("io.netty:netty-codec-socks:${project.netty_version}") { transitive = false }

    modInclude "io.gitlab.jfronny.libjf:libjf-unsafe-v0:${project.libjf_version}"
    include "io.gitlab.jfronny.libjf:libjf-base:${project.libjf_version}"
    modLocalRuntime "io.gitlab.jfronny.libjf:libjf-devutil:${project.libjf_version}"
}

loom {
    accessWidenerPath = file("src/main/resources/meteor-client.accesswidener")
}

shadowJar {
	configurations = [ project.configurations.shadow ]
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding("UTF-8")
}

jar {
	from "LICENSE"
}

remapJar {
	dependsOn shadowJar
	input.set(shadowJar.archiveFile)
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

publishing {
    publications {
        java(MavenPublication) {
            artifactId = "meteor-client"
            version = project.mod_version

            artifact(remapJar) {
                builtBy remapJar
            }

            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }

            artifact(javadocJar) {
                builtBy javadocJar
            }
        }
    }
}
